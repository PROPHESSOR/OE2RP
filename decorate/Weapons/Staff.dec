Actor OE2Staff: OE2BaseWeapon {
	// Called "Guilding staff" in doomwiki.org.
	Weapon.SelectionOrder 1300

	OE2BaseWeapon.HitSound "weapons/staff/hit"
	OE2BaseWeapon.WoodHitSound "weapons/staff/woodhit"

	Tag "$OE2RP_W_STAFF"
	Obituary "TODO:"
	Inventory.PickupMessage "$OE2RP_W_STAFF"

	States {
		Ready:
			STAF A 0
			STAF A 0 A_OE2RP_ClearSuccessfulHits()
			STAF A 0 A_JumpIfInventory( "OE2Staff_LeftItem", 1, 2 )
			STAF A 1 A_WeaponReady
			Wait
			STAF C 1 A_WeaponReady
			Wait
		Deselect:
			STAF A 1 A_Lower
			Wait
		Select:
			STAF A 1 A_Raise
			Wait
		Fire:
			STAF A 0
			STAF A 0 A_JumpIfInventory( "OE2Staff_LeftItem", 1, "FireLeft" )
		FireRight:
			STAF A 3
			STAF B 7 A_OE2RP_WeaponMelee( 5 )
			STAF B 0 A_OE2RP_JumpIfSuccessfulHit( "SetStaffLeft" )
			Goto Ready
		FireLeft:
			STAF C 3
			STAF D 7 A_OE2RP_WeaponMelee( 5 )
			STAF D 0 A_OE2RP_JumpIfSuccessfulHit( "SetStaffRight" )
			Goto Ready
		SetStaffLeft:
			STAF C 4
			STAF A 0 A_GiveInventory( "OE2Staff_LeftItem", 1 )
			Goto Ready
		SetStaffRight:
			STAF A 4
			STAF A 0 A_TakeInventory( "OE2Staff_LeftItem", 0 )
			Goto Ready
		/*AltFire:
			STAF BCDABCD 4
			STAF A 17 A_OE2RP_Massacre
			STAF C 0 A_Refire( "AltFireCont" )
			Goto Ready
		AltFireCont:
			STAF D 10 A_OE2RP_Massacre( true )
			Goto Ready*/
		Spawn:
			TNT1 A -1
			Stop
	}
}

Actor OE2Staff_LeftItem: Inventory {
	Inventory.Amount 1
	Inventory.MaxAmount 1
}
