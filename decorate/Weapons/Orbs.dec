Actor OE2ShrinkOrbWeapon: OE2BaseWeapon {
	//Weapon.AmmoUse 1

	States {
	Deselect:
		ORBH A 1 A_Lower
		Wait
	Select:
		ORBH A 1 A_Raise
		Wait
	Ready:
		ORBH A 0 A_GunFlash( "OrbWeaponFlash" )
		ORBH A 1 A_WeaponReady
		Wait
	Fire:
		ORBH A 2
		ORBH B 1 A_GunFlash
		// Forces VM to re-evalute the state pointer in derived classes (a-la "override"):
		ORBH B 0 A_Jump( 256, "OrbWeaponFire" )
		ORBH B 0 A_Log( "Warning: cannot jump to the state \"OrbWeaponFire\"." )
		Goto Ready
	OrbWeaponFire:
		ORBH B 14 A_OE2RP_WeaponMissile( "OE2ShrinkOrb" )
		ORBH A 5
		Goto Ready
	OrbWeaponFlash:
		ORBS A 5 Bright
		ORBS B 4 Bright
		ORBS C 5 Bright
		ORBS B 4 Bright
		Loop
	Flash:
		ORBH A 0 // Clears an orb picture.
		Stop
	}
}

Actor OE2MoldOrbWeapon: OE2ShrinkOrbWeapon {
	States {
	OrbWeaponFire:
		ORBH B 14 A_OE2RP_WeaponMissile( "OE2MoldOrb" )
		ORBH A 5
		Goto Ready
	OrbWeaponFlash:
		ORBM A 16 Bright
		ORBM A 1 Bright A_Jump( 47, 1 )
		Wait
		ORBM BCB 4 Bright
		Loop
	}
}

Actor OE2FreezeOrbWeapon: OE2ShrinkOrbWeapon {
	States {
	OrbWeaponFire:
		ORBH B 14 A_OE2RP_WeaponMissile( "OE2FreezeOrb" )
		ORBH A 5
		Goto Ready
	OrbWeaponFlash:
		ORBF ABC 4 Bright
		ORBF B 4 Bright A_Jump( 127, "OrbWeaponFlash" )
		Loop
	}
}


// See /zscript/Entities/EntityExtend.zsc for actor "OE2ShrinkOrb" definition:

Actor OE2MoldOrb: OE2ShrinkOrb {
	OE2ShrinkOrb.GiveToVictim "OE2PoisonPowerup"
	RenderStyle "Translucent"
	Alpha 1.0

	States {
	Spawn:
		ORBM D 16 Bright
		ORBM EFE 4 Bright
		Loop
	Death:
		ORBM G 4 Bright A_SetRenderStyle( 1.0, STYLE_Add )
		ORBM G 1 A_FadeOut( 0.05 )
		Wait
	}
}

Actor OE2FreezeOrb: OE2ShrinkOrb {
	OE2ShrinkOrb.GiveToVictim "OE2FreezePowerup"

	States {
	Spawn:
		ORBF DEF 4 Bright
		Loop
	Death:
		ORBF G 4 Bright
		ORBF G 1 A_FadeOut( 0.05 )
		Wait
	}
}
